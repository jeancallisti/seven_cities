// room script file

function region1_Standing()
{
  if(DialogsLogic.GetVarBool("eDialog_hasReadTelegram"))
  {
    player.ChangeRoom(eRoom_02_07_BarnettOutside, 113, 102); //outside
  }
}

function region1_WalksOnto()
{
  if(!DialogsLogic.GetVarBool("eDialog_hasReadTelegram"))
  {
    player.Say("Before leaving the school, i need to find and read marcus telegram");
    player.Walk(586, 119, eBlock);
    return;

    
  }
}

function region3_Standing()
{
  player.ChangeRoom(eRoom_02_05_Irene, 240,  130); //irene
}

function region2_Standing()
{
  player.ChangeRoom(eRoom_02_04_Mulbray, 160,  100); //mulbray's  
}

function region4_Standing()
{
  player.ChangeRoom(eRoom_02_02_IndyClassRoom_New, 160,  100); //indy's
}

function oOilCan_AnyClick()
{
  player.Walk(oOilCan.X,  oOilCan.Y+30,  eBlock);
  player.FaceDirection(eRight);
  
  if (game.used_mode == eModeLookat) {
    player.Say("That's an oil can");

  } else if (game.used_mode == eModePickup) {  
    player.Say("I'll just take it.");
    oOilCan.Visible = false;
    player.AddInventory(iOilCan);
  } else {
    Verbs.Unhandled(eGizmo);
  }  
}

function room_Load()
{
  DownHill.MakeDownhill(4, 110, 117, 85, 95); //make walkable area 4 a downhill
  DownHill.SetWalkableZ(4, 1); //give WA #4 a z-order==1
  DownHill.SetWalkableZ(1, 3); //give WA #1 a z-order==3 (i.e. in front of WA #4)
  DownHill.SetWalkBehindZ(1, 2, 110); //give walkbehind #1 a z-order==2 (in front of WA #4 but under WA #1)
}

function room_FirstLoad()
{
  //TODO: play credits when Indy exists classroom
  //(disabled for now, for tests purposes)
  /*
  #ifdef DEMO
  Wait (60);
  IntroCredits_CreditSectionStart(5);
  Wait (40);
  QuitGame (0);
  #endif
*/

  PlayerUI.EnableAndShow(); 

  #ifdef TESTING
  if (CheckPoints.IsJumping()) { //we're in debug and we arrived in this room using the "jump to checkpoint" button
     
    CheckPoints.JumpDone();
    
    if (CheckPoints.RequestedCheckpoint(eRoom_02_03_BarnettCorridor)) {
      player.AddInventory(iOilCan);
      player.AddInventory(iCompass);
      player.AddInventory(iCompassNote);
      player.AddInventory(iTelegram);
      DialogsLogic.SetVarBool("eDialog_FoundProperSubject",  true);
      DialogsLogic.SetVarBool("eDialog_LookedAtCompassTag",  true);    
      DialogsLogic.SetVarBool("eDialog_SandyHasExplainedTheProblem",  true);    
      DialogsLogic.SetVarBool("eDialog_ShowedCompassToStudents",  true);    
      DialogsLogic.SetVarBool("eDialog_StudentsPuzzleFullyResolved",  true); 
      DialogsLogic.SetVarBool("eDialog_hasReadTelegram",  true); 
      DialogsLogic.SetVarBool("eDialog_BulliesHaveSlashedTire",  true); 
    }
  }
  #endif
  
}
